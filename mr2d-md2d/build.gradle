apply plugin: 'com.android.library'
apply plugin: "maven-publish"

group = "eu.interoperhate"
version="0.0.3"

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName version
        setProperty("archivesBaseName", "$archivesBaseName-$version")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def FHIR_VERSION = "4.1.0"
    api "ca.uhn.hapi.fhir:hapi-fhir-android:$FHIR_VERSION"
    api "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$FHIR_VERSION"
}

/*
 * Configuration of a task, for publishing mrd2-md2d.aar into a Nexus Repository
 */
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "r2d-d2d-commons"
            artifact("build/outputs/aar/$archivesBaseName"+"-release.aar") {
                extension "aar"

                // adds dependencies to the generated POM
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    //Iterate over the dependencies, adding a <dependency> node for each
                    configurations.releaseCompileClasspath.allDependencies.each {
                        if (it.group != null && it.name != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)

                            //If there are any exclusions in dependency
                            if (it.excludeRules.size() > 0) {
                                def exclusionsNode = dependencyNode.appendNode('exclusions')
                                it.excludeRules.each { rule ->
                                    def exclusionNode = exclusionsNode.appendNode('exclusion')
                                    exclusionNode.appendNode('groupId', rule.group)
                                    exclusionNode.appendNode('artifactId', rule.module)
                                }
                            }
                        }
                    }
                }
            }

            // descriptive information to add to the generated POM file
            pom {
                name = 'R2D-D2D Library'
                description = 'Android library (developed by InteropEHRate consortium) for common classes shared' +
                        'by R2D and D2D.'
                url = 'http://www.interopehrate.eu'
            }
        }
    }

    repositories {
        maven {
            name "IEHR-Nexus"
            url "http://213.249.46.206:8081/repository/maven-releases/"
            credentials {
                username project.repoUser
                password project.repoPassword
            }
        }
    }
}
